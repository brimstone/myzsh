# our precmd runs first, as it build up the PROMPT variable
function precmd_main {
	###
	# Decide whether to set a screen title
	if [ -n "$STY" -o -n "$TMUX" ]; then
		TITLEOUTPUT=""
		for ((i=1; i <= ${#title_funcs}; i+=1)); do
			$title_funcs[$i] | read -r OUTPUT
			[ -n "$OUTPUT" ] && TITLEOUTPUT+="${OUTPUT}:"
		done
		printf "\x1bk%s\x1b\\" "${TITLEOUTPUT%:}"
	fi
	# Now that we have all of our variables set, let our theme compute it's prompt
	theme_prompt
}

function precmd {
	# Capture our exit code
	local exitcode=$?
	# run all of our precmd functions setup for us
	local i
	for ((i=1; i <= ${#precmd_funcs}; i+=1)); do
		$precmd_funcs[$i]
	done
}

function preexec {
	# run all of our preexec functions
	# this is off by 1 because preexec_funcs is initalized with nothing
	local i
	for ((i=1; i <= ${#preexec_funcs}; i+=1)); do
		$preexec_funcs[$i] "$1"
	done
}

function myzsh {
	# TODO need other verbs
	# TODO check to make sure $2 is sane
	if [ "$1" = "edit" ]; then
		[ ! -e "$BASE/modules/$2/$2" ] && echo "Cannot find module $2" && return 1
		$EDITOR "$BASE/modules/$2/$2"
	elif [ "$1" = "reload" ]; then
		[ ! -e "$BASE/modules/$2/$2" ] && echo "Cannot find module $2" && return 1
		source "$BASE/modules/$2/$2"
	fi
}
# This is just the tab completion stuff. I wonder if it can be merged into myzsh() ?
function _myzsh {
	local -a verbs
	verbs=(
		'list:List modules'
		'edit:Edit a module'
		'reload:Reload modules'
	)
	# words[2] is whatever is already typed into the command line after "myzsh "
	case "$words[2]" in
	edit)
		_files -W "$BASE/modules/";;
	reload)
		_files -W "$BASE/modules/";;
	*)
		_describe -t commands 'myzsh commands' verbs -V1;;
	esac
	return 1
}

# basic system detection
function islinux {
	[ $(uname) = "Linux" ]
}

function isosx {
	[ $(uname) = "Darwin" ]
}

# first Set any variables
preexec_funcs=()
precmd_funcs=(precmd_main)
# then load the theme
# check for theme, select our default if nothing's set
[ ! -e "$BASE/themes/$THEME/$THEME" ] && THEME="default"
source "$BASE/themes/$THEME/$THEME"
# set other globals
[ -z "$EDITOR" ] && export EDITOR=vim && bindkey -e

# Load any one time modules
for m in $EXTRA; do
	OUTPUT=""
	myzsh reload "$m"
done


lp_funcs=()
for m in $LPRIMARY; do
	OUTPUT=""
	myzsh reload "$m" && lp_funcs+=($OUTPUT)
done
# Figure out our left secondary output
ls_funcs=()
for m in $LSECONDARY; do
	OUTPUT=""
	myzsh reload "$m" && ls_funcs+=($OUTPUT)
done
# Figure out our right primary output
rp_funcs=()
for m in $RPRIMARY; do
	OUTPUT=""
	myzsh reload "$m" && rp_funcs+=($OUTPUT)
done
# Figure out our right secondary output
rs_funcs=()
for m in $RSECONDARY; do
	OUTPUT=""
	myzsh reload "$m" && rs_funcs+=($OUTPUT)
done

# Figure out our title output
title_funcs=()
for m in $TITLE; do
	OUTPUT=""
	myzsh reload "$m" && title_funcs+=($OUTPUT)
done

# TODO make sure our completion stuff is loaded so this works
# our completion should be loaded by now so we can link our completion function to our management function
compdef _myzsh myzsh
